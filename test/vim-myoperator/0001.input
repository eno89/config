" Tests for myoperator
" Set up  "{{{1

function Call(function_name, ...)
  return call(substitute(a:function_name, '^s:', myoperator#_sid_prefix(), ''),
  \           a:000)
endfunction

function! s:SID_PREFIX()
  return matchstr(expand('<sfile>'), '\%(^\|\.\.\)\zs<SNR>\d\+_\zeSID_PREFIX$')
endfunction




function s:test_internal_key_mappings_0001()  "{{{1
  Title "Check the existence of <SID>(count) in each mode"

  let _ = myoperator#_sid_prefix().'(count)'
  Assert maparg(_, 'c') != '' ==> 0
  Assert maparg(_, 'i') != '' ==> 0
  Assert maparg(_, 'n') != '' ==> 1
  Assert maparg(_, 'o') != '' ==> 0
  Assert maparg(_, 'v') != '' ==> 1
endfunction




function s:test_myoperator_define_0001()  "{{{1
  function! TestOperatorFunction1(motion_wiseness)
    " echo getpos("'[") getpos("']")
    '[,']sort
  endfunction

  noremap _  xxx
  Title DefineOperator _  TestOperatorFunction1

  Assert maparg('_', 'c') != '' ==> 0
  Assert maparg('_', 'i') != '' ==> 0
  Assert maparg('_', 'n') != '' ==> 1
  Assert maparg('_', 'o') != '' ==> 1
  Assert maparg('_', 'v') != '' ==> 1

  Assert maparg('_', 'n') =~# '\<TestOperatorFunction1\>' ==> 1
  Assert maparg('_', 'o') == 'g@' ==> 1
  Assert maparg('_', 'v') =~# '\<TestOperatorFunction1\>' ==> 1

  silent put =['a', 'm', 'a', 'n', 'e']
  -4
  normal _4j
  Assert getline('.', line('.')+4) ==> ['a', 'a', 'e', 'm', 'n']

  silent put =['a', 'v', 'r', 'i', 'l']
  -4
  normal v4j_
  Assert getline('.', line('.')+4) ==> ['a', 'i', 'l', 'r', 'v']

  silent put =['a', 'g', 'n', 'e', 's']
  -4
  normal _5_
  Assert getline('.', line('.')+4) ==> ['a', 'e', 'g', 'n', 's']

  " FIXME: This one works as I expect if '5__' is typed interactively,
  "        but it doesn't work via :normal and it selects only the current
  "        line.  Why?
  " silent put =['a', 'g', 'n', 'e', 's']
  " -4
  " normal 5__
  " Assert getline('.', line('.')+4) ==> ['a', 'e', 'g', 'n', 's']
endfunction




function s:test_myoperator_define_0002()  "{{{1
  Title DefineOperator _  TestOperatorFunction2
  \     call TestOperatorFunction2Set('yank')
  function! TestOperatorFunction2Set(command)
    let s:command = a:command
  endfunction
  function! TestOperatorFunction2(motion_wiseness)
    execute "'[,']" s:command
  endfunction

  silent put =['a', 'z', 'u', 's', 'a']
  -4

  silent normal __
  Assert @0 ==> "a\n"

  silent normal _2j
  Assert @0 ==> "a\nz\nu\n"

  silent normal v4j_
  Assert @0 ==> "a\nz\nu\ns\na\n"
endfunction




function s:test_myoperator_define_0003()  "{{{1
  execute 'Title DefineOperator @  '.s:SID_PREFIX().'test_operator_function_3'
  function! s:test_operator_function_3(motion_wiseness)
    " echo getpos("'[") getpos("']")
    '[,']sort
  endfunction

  Assert maparg('@', 'c') != '' ==> 0
  Assert maparg('@', 'i') != '' ==> 0
  Assert maparg('@', 'n') != '' ==> 1
  Assert maparg('@', 'o') != '' ==> 1
  Assert maparg('@', 'v') != '' ==> 1

  Assert maparg('@', 'n') =~# 'test_operator_function_3\>' ==> 1
  Assert maparg('@', 'o') == 'g@' ==> 1
  Assert maparg('@', 'v') =~# 'test_operator_function_3\>' ==> 1

  silent % delete _

  silent put =['a', 'm', 'a', 'n', 'e']
  -4
  normal @4j
  Assert getline('.', line('.')+4) ==> ['a', 'a', 'e', 'm', 'n']

  silent put =['a', 'v', 'r', 'i', 'l']
  -4
  normal v4j@
  Assert getline('.', line('.')+4) ==> ['a', 'i', 'l', 'r', 'v']

  silent put =['a', 'g', 'n', 'e', 's']
  -4
  normal @5@
  Assert getline('.', line('.')+4) ==> ['a', 'e', 'g', 'n', 's']

  " FIXME: This one works as I expect if '5@@' is typed interactively,
  "        but it doesn't work via :normal and it selects only the current
  "        line.  Why?
  " silent put =['a', 'g', 'n', 'e', 's']
  " -4
  " normal 5@@
  " Assert getline('.', line('.')+4) ==> ['a', 'e', 'g', 'n', 's']
endfunction




Test  "{{{1

" __END__
" vim: filetype=vim foldmethod=marker
